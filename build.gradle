import com.example.compose.jetchat.buildsrc.Libs
import com.example.compose.jetchat.buildsrc.Urls

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath Libs.androidGradlePlugin
        classpath Libs.Kotlin.gradlePlugin
    }
}

plugins {
    id 'com.diffplug.spotless' version '6.7.0'
}
subprojects {
    repositories {
        google()
        mavenCentral()

        if (!Libs.AndroidX.Compose.Material3.snapshot.isEmpty()) {
            maven { url Urls.composeMaterial3SnapshotRepo }
        }
        if (!Libs.AndroidX.Compose.snapshot.isEmpty()) {
            maven { url Urls.composeSnapshotRepo }
        }
    }

    apply plugin: 'com.diffplug.spotless'
    spotless {
        kotlin {
            target '**/*.kt'
            targetExclude("$buildDir/**/*.kt")
            targetExclude('bin/**/*.kt')

            ktlint("0.45.2")
            //licenseHeaderFile rootProject.file('spotless/copyright.kt')
        }
    }
}
